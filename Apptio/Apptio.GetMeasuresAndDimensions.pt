let 
    doc = [
        Documentation.Name = "Get the measures and dimensions in Apptio", 
        Documentation.FunctionName = "Apptio.GetMeasuresAndDimensions",
        Documentation.Description = "Get the measure and dimension information in Apptio.", 
        Documentation.Examples = {
            [
                Description = "Retrieve the measures and dimensions",
                Code = "FunctionName()",
                Result = "Returns a information for the measures and dimensionss, like name, type, and groupings."
            ]
        }
    ],
    fn = ()=>
let
   /*  Define API endpoint
            - The API endpoint for EU instances is: https://api-eu.cloudability.com
            - The API endpoint for APAC instances: https://api-au.cloudability.com
            - The API endpoint for ME instances is: https://api-me.cloudability.com
            - The API endpoint for other regions is: https://api.cloudability.com 
        */

    api = "https://api.cloudability.com/v3",

    GetDimensionsAndMeasures = 
        Table.FromList(
            Json.Document(Web.Contents(api,[RelativePath="/reporting/cost/measures"])), 
            Splitter.SplitByNothing(), null, null, ExtraValues.Error
        ),
    
    GetResultColNames = List.Distinct(
        List.Combine(
            List.Transform(Table.ToRecords(GetDimensionsAndMeasures), each Record.FieldNames(Record.Field(_, "Column1")))
        )
    ),

    ExpandRecords = Table.ExpandRecordColumn(GetDimensionsAndMeasures,"Column1",GetResultColNames),

    GetGroupColNames = 
        List.Distinct(
            List.Combine(
                List.Transform(Table.ToRecords(ExpandRecords), each Record.FieldNames(Record.Field(_, "group")))
            )
        ),

    ExpandGroup = 
        Table.ExpandRecordColumn(
            ExpandRecords,"group",GetGroupColNames, List.Transform(GetGroupColNames, each "group." & _)
        ),

    GetSubGroupColNames = 
        List.Distinct(
            List.Combine(
                List.Transform(Table.ToRecords(ExpandGroup), each Record.FieldNames(Record.Field(_, "sub_group")))
            )
        ),

    ExpandSubGroup = 
        Table.ExpandRecordColumn(
            ExpandGroup,"sub_group",GetSubGroupColNames, List.Transform(GetSubGroupColNames, each "sub_group." & _)
        )          

in
    ExpandSubGroup
in 
    Value.ReplaceType(fn, Value.ReplaceMetadata(Value.Type(fn), doc))
